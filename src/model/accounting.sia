/**
@dev Ed25519 Signature
*/
schema Signature {
  signer    byteN(length = 32)
  signature byteN(length = 64)
}

/**
@dev A credit request.

Adds the `amount` to user's account in `currency`.
Fails if the uuid is not unique.
*/
schema Credit {
  uuid      byte8
  amount    uint64
  currency  string8(encoding = "ascii")
  user      byteN(length = 32)
  subnet    byteN(length = 32)
  proof     Signature
}

/**
@dev A refund request.

Refunds the `amount` to user's account in `currency`.
Fails if the uuid is not an existing credit request.
*/
schema Refund {
  uuid      byte8
  amount    uint64
  currency  string8(encoding = "ascii")
  user      byteN(length = 32)
  subnet    byteN(length = 32)
  proof     Signature
}

/**
@dev A debit request. Should contain the hash
of the user request and their signature.

Debits the `amount` from user's account in `currency`,
fails if the user balance drops below zero.

Fails if the uuid is not unique.
*/
schema Debit {
  uuid      byte8
  amount    uint64
  currency  string8(encoding = "ascii")
  user      Signature
  subnet    byteN(length = 32)
  proof     Signature
}

/**
@dev Authorize an additional identity
to sign requests on behalf of the subnet.
*/
schema Authorize {
  user    byteN(length = 32)
  subnet  byteN(length = 32)
  proof   Signature
}

/**
@dev Unauthorize an identity
to sign requests on behalf of the subnet.
*/
schema UnAuthorize {
  user    byteN(length = 32)
  subnet  byteN(length = 32)
  proof   Signature
}

/**
@dev A function call request.
The `plugin` and `method` fields are strings
that specify the plugin and method to be called.
The `uuid` field is a unique identifier for the request.
*/
schema FunctionCall {
  uuid      byte8
  plugin    string8(encoding = "ascii")
  method    string8(encoding = "ascii")
}

/**
@dev A function call response.
The `uuid` field is a unique identifier for the request.
The `error` field is an optional error code.
*/
schema Error {
  uuid        byte8
  error       uint16
}

/**
@dev A function call response.
The `uuid` field is a unique identifier for the request.
The `error` field is an optional error code.
The `status` field indicates whether the request was successful.
*/
schema Success {
  uuid        byte8
  error?      uint16
  status      bool
}
